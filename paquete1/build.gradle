apply plugin: "maven-publish"
apply plugin: "com.android.library"
configurations.maybeCreate("default")

//MODIFICAR COMIENZO

//Indicar el nombre del aar que vamos a subir
def publishArtifact = artifacts.add("default", file('file.aar'))

//El group id, suele ser com.facephi
//El group id, suele ser com.facephi
def group_id = 'com.facephi'

//id del artefacto
def artifactid = 'paquete1'

//version que queremos subir del mismo
version = '1.0.0'

//El paquete final que se subiría siguiendo esta configuración quedaría tal que
// com.facephi:paquete1:1.0.0


dependencies {
    //Aqui se indicarán las dependencias en caso de necesitar
    //Las dependencias se importan como en el ejemplo de abajo
    /*
    implementation "androidx.multidex:multidex:2.0.1"
    */
}

//MODIFICAR FINAL


android {
    compileSdkVersion 30
    buildToolsVersion "30.0.0"

    lintOptions {
        abortOnError false
    }
    defaultConfig {

        minSdkVersion 19
        targetSdkVersion 30
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId group_id
            artifactId artifactid
            artifact publishArtifact
            pom {
                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    def configurations = [
                            project.configurations.implementation,
                            project.configurations.api
                    ]

                    configurations.each { configuration ->
                        configuration.allDependencies.each {
                            if (it.name != 'unspecified' && it.group != rootProject.name) {
                                def dependencyNode = dependenciesNode.appendNode('dependency')
                                dependencyNode.appendNode('groupId', it.group)
                                dependencyNode.appendNode('artifactId', it.name)
                                dependencyNode.appendNode('version', it.version)
                            }
                        }
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/facephi/central-packages")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
}

